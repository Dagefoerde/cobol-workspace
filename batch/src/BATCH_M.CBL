       IDENTIFICATION DIVISION.
       PROGRAM-ID. BATCH_M.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
       SELECT TRANSACTION-IN ASSIGN TO "input.txt"
           ORGANIZATION IS LINE SEQUENTIAL       
           FILE STATUS IS TRANSACTION-IN-FILE-STATUS.
       SELECT PROCESSING-OUT ASSIGN TO 'output.txt' 
           ORGANIZATION IS LINE SEQUENTIAL
           FILE STATUS IS PROCESSING-OUT-FILE-STATUS.
       DATA DIVISION.
       FILE SECTION.
       FD TRANSACTION-IN.
       01  TRANSACTION-F.
         05  BANKID-F    PIC 9(5).
         05  ACCOUNTID-F PIC 9(10).
         05  AMOUNT-F    PIC Z(7)9V99.
         05  COMMENT-F   PIC X(55).
       FD PROCESSING-OUT.
       01  PROCESSING-OUT-F.
         05  DESCRIPTION-F PIC X(21).
         05  OUTSALDO-F    PIC Z(08)9.99.
       WORKING-STORAGE SECTION.
       01  TRANSACTION.
         05  BANKID      PIC 9(5).
         05  ACCOUNTID   PIC 9(10).
         05  AMOUNT      PIC 9(8)V99.
         05  COMMENT     PIC X(55).
       01  READSTATUS    PIC X VALUE SPACE.
         88  EOF         VALUE "X".
       01  FILE-STATUS.
         05  TRANSACTION-IN-FILE-STATUS   PIC X(02).
         05  PROCESSING-OUT-FILE-STATUS   PIC X(02).
       01  CURRENTBANK.
         05  CBID      PIC 9(5) VALUE 0.
         05  CBSALDO   PIC 9(8)V99 VALUE 0.
       01  CURRENTACCOUNT.
         05  CAID      PIC 9(10) VALUE 0.
         05  CASALDO   PIC 9(8)V99 VALUE 0.
       01  ACCOUNTDESC PIC X(21) VALUE "Account XXXXXXXXXX:  ".
       01  BANKDESC    PIC X(21) VALUE "Total EUR for XXXXX: ".

       PROCEDURE DIVISION.
           OPEN OUTPUT PROCESSING-OUT
           OPEN INPUT TRANSACTION-IN
              PERFORM UNTIL EOF 
                 READ TRANSACTION-IN INTO TRANSACTION
                   AT END SET EOF TO TRUE
                   NOT AT END PERFORM PROCESS-TRANSACTION
                 END-READ
              END-PERFORM
           CLOSE TRANSACTION-IN
           PERFORM CLOSE-BANK
           CLOSE PROCESSING-OUT
           GOBACK
           .
       
       PROCESS-TRANSACTION SECTION.
           IF CBID NOT = BANKID THEN
               PERFORM CLOSE-BANK
               MOVE BANKID TO CBID
           END-IF
           IF CAID NOT = ACCOUNTID THEN
               PERFORM CLOSE-ACCOUNT
               MOVE ACCOUNTID TO CAID
           END-IF


           ADD AMOUNT TO CBSALDO
           ADD AMOUNT TO CASALDO
           .
       
       CLOSE-BANK SECTION.
           IF CBID EQUAL 0 THEN
               EXIT SECTION
           END-IF

           PERFORM CLOSE-ACCOUNT
      *    DISPLAY "================================"
      *    DISPLAY "Total EUR for " CBID ": " CBSALDO
      *    DISPLAY SPACE

           WRITE PROCESSING-OUT-F FROM
               "================================="
                   
           MOVE CBID TO BANKDESC(15:5)
           MOVE BANKDESC TO DESCRIPTION-F
           MOVE CBSALDO TO OUTSALDO-F
           WRITE PROCESSING-OUT-F FROM PROCESSING-OUT-F


           WRITE PROCESSING-OUT-F FROM SPACE

           INITIALIZE CURRENTBANK
           .

       CLOSE-ACCOUNT SECTION.
           IF CAID EQUAL 0 THEN
               EXIT SECTION
           END-IF

      *    DISPLAY "Account " CAID ":  " CASALDO
           MOVE CAID TO ACCOUNTDESC(9:10)
           MOVE ACCOUNTDESC TO DESCRIPTION-F
           MOVE CASALDO TO OUTSALDO-F
           WRITE PROCESSING-OUT-F FROM PROCESSING-OUT-F

           INITIALIZE CURRENTACCOUNT
           .